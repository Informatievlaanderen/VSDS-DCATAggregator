prefix qb:   <http://purl.org/linked-data/cube#> 
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
prefix sh:   <http://www.w3.org/ns/shacl#> 
prefix skos: <http://www.w3.org/2004/02/skos/core#> 
prefix v:    <http://www.w3.org/2006/vcard/ns#> 
prefix vl:   <https://data.vlaanderen.be/ns/shacl#> 
prefix dcat: <http://www.w3.org/ns/dcat#> 
prefix dct:  <http://purl.org/dc/terms/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
DELETE {
  # /1
  ?set <http://purl.org/dc/terms/license> "https://creativecommons.org/licenses/by/4.0/"^^<http://www.w3.org/2001/XMLSchema#anyURI> . 
  # /2
  ?ds_kw <http://www.w3.org/ns/dcat#keyword> ?kw .
  # /3
  ?ds_ep <http://www.w3.org/ns/dcat#endpointURL> ?ep_url .
  # /4
  ?ds_title <http://purl.org/dc/terms/title> ?title .
  # /6
  ?id_des <http://purl.org/dc/terms/description> ?desc .
  # /7
  ?uc_7 <http://www.w3.org/ns/dcat#endpointDescription> ?uc_7_existing_value .
  # /9
  ?uc_9_pub a ?uc_9_pub_type .
  # /10
  ?uc_10_pub <http://xmlns.com/foaf/0.1/name> ?uc_10_name .
}

INSERT {
  # /1
  ?set <http://purl.org/dc/terms/license> <https://data.vlaanderen.be/id/licentie/creative-commons-zero-verklaring/v1.0> . 
  # /2
  ?ds_kw <http://www.w3.org/ns/dcat#keyword> ?kw_en . 
  # /3
  ?ds_ep <http://www.w3.org/ns/dcat#endpointURL> ?epd_url_lit .
  # /4
  ?ds_title <http://purl.org/dc/terms/title> ?title_en .
  # /5
  ?ds_ar <http://purl.org/dc/terms/accessRights> <http://publications.europa.eu/resource/authority/access-right/PUBLIC> .
  # /6
  ?id_des <http://purl.org/dc/terms/description> ?desc_en .
  # /7
  ?uc_7 <http://www.w3.org/ns/dcat#endpointDescription> ?uc_7_value .
  # /8
  ?ds dct:identifier ?identifier .
  # /9
  ?uc_9_pub a <http://purl.org/dc/terms/Agent> .
  # /10 
  ?uc_10_pub <http://xmlns.com/foaf/0.1/name> ?uc_10_name_en .
  # /11
  ?cat_s ?cat_p ?cat_o .
}
WHERE {
  # 1/ Set license on catalogs and distribustions.
  {
    VALUES ?l_data_type { <http://www.w3.org/ns/dcat#Dataset> <http://www.w3.org/ns/dcat#DataService> } .
    ?set a ?l_data_type .
    ?set <http://purl.org/dc/terms/license> "https://creativecommons.org/licenses/by/4.0/"^^<http://www.w3.org/2001/XMLSchema#anyURI> .
  } UNION
  # 2/ keywords should be language literals.
  {
    ?ds_kw <http://www.w3.org/ns/dcat#keyword> ?kw .
    FILTER (!langMatches( lang(?kw), "*" )) .
    BIND (STRLANG(?kw, "en") AS ?kw_en) .
  } UNION
  # 3/ endpointURL should be literal of type anyURI instead of resource.
  {
    ?ds_ep <http://www.w3.org/ns/dcat#endpointURL> ?ep_url .
    #BIND (STRDT(STR(?ep_url), xsd:anyURI) as ?ep_url_lit ) .
    BIND (STRDT(STR(?ds_ep), xsd:anyURI) as ?epd_url_lit ) .
  } UNION
  # 4/ Titles should be language literals.
  {
    ?ds_title <http://purl.org/dc/terms/title> ?title .
    FILTER (!langMatches( lang(?title), "*" )) .
    BIND (STRLANG(?title, "en") AS ?title_en) .
  } UNION
  # 5/ Datasets and dataservices need to declare access rights (public or non-public).
  {
    VALUES ?ar_type { <http://www.w3.org/ns/dcat#Dataset> <http://www.w3.org/ns/dcat#DataService> } .
    ?ds_ar a ?ar_type .
    OPTIONAL { ?ds_ar <http://purl.org/dc/terms/accessRights> ?ar .}
    FILTER (!BOUND(?ar))
  } UNION
  # 6/ Descriptions should be language literals.
  {
    VALUES ?desc_type { <http://www.w3.org/ns/dcat#Dataset> <http://www.w3.org/ns/dcat#DataService> } .
    ?id_des a ?desc_type .
    ?id_des <http://purl.org/dc/terms/description> ?desc .
    FILTER (!langMatches( lang(?desc), "*" )) .
    BIND (STRLANG(?desc, "en") AS ?desc_en) .
  } UNION
  # 7/ Dataservices require endpoint description.
  {
    ?uc_7 a <http://www.w3.org/ns/dcat#DataService> .
    OPTIONAL { ?uc_7 <http://www.w3.org/ns/dcat#endpointDescription> ?uc_7_existing_value } .
    BIND (STRDT("http://placeholder/fixme", xsd:anyURI) AS ?uc_7_value ).
  } UNION
  # 8/ Catalogs, datasets and dataservices require a dct:identifier.
  {
    VALUES ?uc_8_type {dcat:Dataset dcat:Catalog dcat:DataService} .
    ?ds a ?uc_8_type .
    OPTIONAL {?ds dct:identifier ?uc_8_id }
    FILTER(!bound(?uc_8_id) )
    BIND(STR(?ds) as ?identifier)
  } UNION
  # 9/ Agents need to be instances of dct:Agent
  # (also, only catalogs have a publisher!)
  {
    ?uc_9_id a <http://www.w3.org/ns/dcat#Catalog> .
    ?uc_9_id <http://purl.org/dc/terms/publisher> ?uc_9_pub .
    ?uc_9_pub a ?uc_9_pub_type .
  } UNION
  # 10/ Agent name should be language literal.
  {
    ?uc_10_id a <http://www.w3.org/ns/dcat#Catalog> .
    ?uc_10_id <http://purl.org/dc/terms/publisher> ?uc_10_pub .
    ?uc_10_pub <http://xmlns.com/foaf/0.1/name> ?uc_10_name .
    BIND (STRLANG(?uc_10_name, "en") AS ?uc_10_name_en) .
  } UNION
  # 11/ Add a catalog...
  {
    VALUES (?cat_s ?cat_p ?cat_o) {
      (<https://marineinfo.org/id/catalog> <http://www.w3.org/1999/02/22-rdf-syntax-ns#type> <http://www.w3.org/ns/dcat#Catalog> )
	(<https://marineinfo.org/id/catalog> <http://purl.org/dc/terms/description> "Catalog description"@en )
	(<https://marineinfo.org/id/catalog> <http://purl.org/dc/terms/identifier> "https://marineinfo.org/id/catalog"^^<http://www.w3.org/2001/XMLSchema#anyURI> )
	(<https://marineinfo.org/id/catalog> <http://purl.org/dc/terms/license> <https://data.vlaanderen.be/id/licentie/creative-commons-zero-verklaring/v1.0> )
	(<https://marineinfo.org/id/catalog> <http://purl.org/dc/terms/title> "Catalog title"@en )
	(<https://marineinfo.org/id/catalog> <http://purl.org/dc/terms/publisher> <https://marineinfo.org/id/agent> )
	(<https://marineinfo.org/id/catalog> <http://www.w3.org/ns/dcat#dataset> <https://marineinfo.org/id/dataset/6293> )
	(<https://marineinfo.org/id/catalog> <http://www.w3.org/ns/dcat#dataset> <https://marineinfo.org/id/dataset/1426> )
	(<https://marineinfo.org/id/catalog> <http://www.w3.org/ns/dcat#service> <https://marineinfo.org/id/dataset/1426#MR_OGCservice> )
	(<https://marineinfo.org/id/catalog> <http://www.w3.org/ns/dcat#service> <https://marineinfo.org/id/dataset/1426#MR_GeoRSS> )
	(<https://marineinfo.org/id/catalog> <http://www.w3.org/ns/dcat#service> <https://marineinfo.org/id/dataset/1426#MR_searchpage> )
	(<https://marineinfo.org/id/catalog> <http://www.w3.org/ns/dcat#service> <https://marineinfo.org/id/dataset/1426#MR_LDES> )
	(<https://marineinfo.org/id/catalog> <http://www.w3.org/ns/dcat#service> <https://marineinfo.org/id/dataset/1426#MR_SOAPservice> )
	(<https://marineinfo.org/id/catalog> <http://www.w3.org/ns/dcat#service> <https://marineinfo.org/id/dataset/1426#MR_RESTservice> )
	(<https://marineinfo.org/id/catalog> <http://www.w3.org/ns/dcat#service> <https://marineinfo.org/id/dataset/1426#MR_browsepage> )
	(<https://marineinfo.org/id/catalog> <http://www.w3.org/ns/dcat#service> <https://marineinfo.org/id/dataset/1426#MR_mregions> )
	( <https://marineinfo.org/id/contact> <http://www.w3.org/1999/02/22-rdf-syntax-ns#type> <http://www.w3.org/2006/vcard/ns#Kind> )
	( <https://marineinfo.org/id/contact> <http://www.w3.org/2006/vcard/ns#hasEmail> "info@marineregions.org"^^<http://www.w3.org/2001/XMLSchema#anyURI> )
	( <https://marineinfo.org/id/contact> <http://www.w3.org/2006/vcard/ns#hasURL> "https://www.marineregions.org/"^^<http://www.w3.org/2001/XMLSchema#anyURI> )
	( <https://marineinfo.org/id/contact> <http://www.w3.org/2006/vcard/ns#organizationname> "Vlaams Instituut voor de Zee (VLIZ)" )
	(<https://marineinfo.org/id/catalog> <http://www.w3.org/ns/dcat#contactPoint> <https://marineinfo.org/id/contact> )
        (<https://marineinfo.org/id/agent> <http://www.w3.org/1999/02/22-rdf-syntax-ns#type> <http://purl.org/dc/terms/Agent> )
	(<https://marineinfo.org/id/agent> <http://schema.org/identifier> "ISNI:0000000122309672" )
	(<https://marineinfo.org/id/agent> <http://xmlns.com/foaf/0.1/name> "Flanders Marine Institute"@en )

    }

  }
}

